apply plugin: 'android'
apply plugin: 'maven'

def genDir = "$buildDir/generated"

def genSrcDir = "$genDir/java"

def genResDir = "$genDir/resources"

def cedictDataPath = "$genResDir/cedict-data.txt"

def cedictCompressedDataPath = "cedict-data.gz"

def cedictDataUrl = 'http://www.mdbg.net/chindict/export/cedict/cedict_1_0_ts_utf-8_mdbg.txt.gz'

configurations {
	generate
}

dependencies {
	compile project(':org.juzidian.cedict')
	compile 'com.google.inject:guice:3.0:no_aop'
	compile 'com.j256.ormlite:ormlite-core:4.42'
	generate project(':org.juzidian.build.pinyin')
}

sourceSets {
    main {
        java {
            srcDirs "$genSrcDir" 
        }
        resources {
        	srcDirs "$genResDir"
    	}
    }
}

task fetchCedictData {
	ext.srcUrl = "$cedictDataUrl"
	ext.destFile = file("$cedictCompressedDataPath")
	inputs.property 'srcUrl', srcUrl
	outputs.file destFile
	doLast {
		def outputStream = new FileOutputStream(destFile)
		println "Downloading CEDict data from $srcUrl"
		new URL("$srcUrl").withInputStream({
			outputStream << it
		})
	}
}

task uncompressCedictData {
	dependsOn fetchCedictData
	ext.srcFile = file("$cedictCompressedDataPath")
	ext.destFile = file("$cedictDataPath")
	inputs.file srcFile
	outputs.file destFile
	doLast {
		file(destFile.parent).mkdirs()
		println "Uncompressing $srcFile to $destFile"
		def inputStream = new java.util.zip.GZIPInputStream(new FileInputStream(srcFile))
		new FileOutputStream(destFile) << inputStream
	}
}

task generateSources (type: JavaExec, dependsOn: uncompressCedictData) {
	ext.srcFile = file("$cedictDataPath")
	ext.destDir = file("$genSrcDir")
	inputs.file srcFile
	outputs.dir destDir
	classpath = configurations.generate
	main = 	'org.juzidian.build.pinyin.PinyinHelperGenerator'
	args srcFile, destDir, 'org.juzidian.core'
}

compileJava.dependsOn generateSources

androidProcessResources.dependsOn createAndroidResourcesDir

def androidProperties = new Properties();
file("${projectDir}/project.properties").withInputStream { androidProperties.load(it) }
androidProperties.each({ project.ext[it.key] = it.value })

[proguard, androidPackage, androidSignAndAlign]*.onlyIf {
	!project.hasProperty('android.library') ||
	!project['android.library']
}

