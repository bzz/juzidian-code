apply plugin: 'java'
apply plugin: 'maven'

ext.dictionaryRegistryServiceUrl = localProperties['dictionaryRegistryServiceUrl'] ?: "http://juzidian.org/dictionaries/"

dependencies {
	compile project(':org.juzidian.pinyin')
	compile project(':org.juzidian.cedict')
	compile project(':org.juzidian.util')
	compile libraries.guice_no_aop
	compile libraries.ormlite_core
	compile libraries.slf4j_api
	testCompile libraries.junit
	testCompile libraries.hamcrest_library
	testCompile libraries.mockito_core
	testCompile libraries.ormlite_jdbc
	testCompile libraries.sqlite_jdbc
	testCompile libraries.url_scheme_registry
}

processResources.configure {
	inputs.property 'dictionaryRegistryServiceUrl', dictionaryRegistryServiceUrl
	filter(org.apache.tools.ant.filters.ReplaceTokens, tokens:[dictionaryRegistryServiceUrl: dictionaryRegistryServiceUrl])
}

eclipse.classpath.file {
	whenMerged { classpath ->
		classpath.entries.removeAll { entry -> entry.path == 'src/main/resources' }
	}
	withXml { provider ->
		def node = provider.asNode()
		node.appendNode('classpathentry', [kind: 'src', path: 'build/resources/main'])
	}
}
