import org.juzidian.build.imggen.AndroidImageResourceGenerator
import org.juzidian.build.imggen.IconDescriptor
import org.juzidian.build.imggen.InputTypeToggleIcon

 apply plugin: 'android'

ext.androidSdkDir = localProperties['sdk.dir']

dependencies {
	compile (project(':org.juzidian.core')) {
		/* slf4j-api conflicts with slf4j-android. */
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.google.inject', module: 'guice'
	}
	compile project(':org.juzidian.pinyin')
	compile project(':org.juzidian.util')
	compile libraries.roboguice
	compile libraries.ormlite_android
	compile libraries.slf4j_android
}

project.ext.androidTargetPlatform = 'android-16'

android {
	compileSdkVersion = "${androidTargetPlatform}"
	defaultConfig {
		versionCode = "${juzidianReleaseSequenceNumber}"
		versionName = "${juzidianVersion}"
		minSdkVersion = 11
		targetSdkVersion = 17
	}
}

def generateImages(resDir) {
	def iconImageGenerator = new AndroidImageResourceGenerator(resDir)
	def icons = [
			new IconDescriptor("search_type_icon_reverse", new InputTypeToggleIcon("En")),
			new IconDescriptor("search_type_icon_pinyin", new InputTypeToggleIcon("æ‹¼"))
	]
	iconImageGenerator.generateIconImages(icons)
}

android.buildVariants.each {
	def generateImagesTaskName = "generateImages${it.name}"
	def resDir = file("build/res/all/${it.baseName}")
	task(generateImagesTaskName) << {
		generateImages(resDir)
	}
	it.processResources.dependsOn generateImagesTaskName
}

eclipse {
	classpath {
		plusConfigurations += configurations.compile
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'src', path: 'src/main/java'])
				node.appendNode('classpathentry', [kind: 'src', path: 'build/source/r/debug'])
				node.appendNode('classpathentry', [kind: 'lib', path: "${androidSdkDir}/platforms/${androidTargetPlatform}/android.jar"])
			}
		}
	}
}
