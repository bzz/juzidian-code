import org.apache.tools.ant.filters.ReplaceTokens
import org.juzidian.build.imggen.AndroidImageResourceGenerator
import org.juzidian.build.imggen.CrossIcon
import org.juzidian.build.imggen.IconDescriptor
import org.juzidian.build.imggen.InputTypeToggleIcon
import org.juzidian.build.imggen.JuzidianLauncherIcon

apply plugin: 'android'

ext.androidSdkDir = localProperties['sdk.dir']
ext.licensesDir = file("build/licenses")
def licensesHtmlTemplateFile = file("src/main/assets/licenses-template.html")
def licensesTextsHtmlFile = file("build/licenses-content.html")

dependencies {
	compile (project(':org.juzidian.core')) {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.google.inject', module: 'guice'
	}
	compile (project(':org.juzidian.dataload')) {
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.google.inject', module: 'guice'
	}
	compile project(':org.juzidian.pinyin')
	compile project(':org.juzidian.util')
	compile libraries.roboguice
	compile libraries.ormlite_android
	compile libraries.slf4j_android
}

project.ext.androidTargetPlatform = 'android-16'

android {
	compileSdkVersion = "${androidTargetPlatform}"
	defaultConfig {
		versionCode = juzidianReleaseSequenceNumber.toInteger()
		versionName = juzidianVersion
		minSdkVersion = 11
		targetSdkVersion = 17
		buildToolsVersion "17.0"
	}
}

def licenses = [
	[
		key: 'gpl',
		title: 'GNU General Public License Version 3',
		textUrl: 'file:COPYING',
	],
	[
		key: 'asl',
		title: 'Apache License Version 2.0',
		textUrl: 'https://www.apache.org/licenses/LICENSE-2.0.txt',
	]
]

def generateImages(resDir) {
	def iconImageGenerator = new AndroidImageResourceGenerator(resDir)
	def icons = [
			new IconDescriptor("search_type_icon_reverse", new InputTypeToggleIcon("En")),
			new IconDescriptor("search_type_icon_pinyin", new InputTypeToggleIcon("æ‹¼")),
			new IconDescriptor("clear_input_icon", new CrossIcon()),
			new IconDescriptor("juzidian_launcher_icon", new JuzidianLauncherIcon())
	]
	iconImageGenerator.generateIconImages(icons)
}

def generateBuildInfoFile(resDir) {
	def rawDir = file("${resDir}/raw")
	rawDir.mkdirs()
	def buildInfoFile = file("${rawDir}/build_info")
	buildInfoFile.withWriter('UTF-8') {
		it.writeLine "timestamp=${buildInfo.buildDate.time}"
		it.writeLine "headCommit=${buildInfo.headCommit}"
	}
}

def licenseFile(license) {
	return file("$licensesDir/${license.key}.txt")
}

def retrieveLicenseTaskName(license) {
	return "retrieveLicense-$license.key"
}

def createHtml(text) {
	return text
		.replaceAll('<', '&lt;')
		.replaceAll('\n\n', '\n<br><br>\n')
}

def createLicenseHtml(license) {
	def licenseText = licenseFile(license).text
	return """
		<h2>$license.title</h2>
		<p>
			${createHtml(licenseText)}
		</p>
		"""
}

licenses.each { license ->
	task (retrieveLicenseTaskName(license)) {
		def licenseFile = licenseFile(license)
		def licenseUrl = license.textUrl
		inputs.property 'url', licenseUrl
		outputs.file licenseFile
		doLast {
			licensesDir.mkdirs()
			def outputStream = new FileOutputStream(licenseFile)
			println "Downloading license text from $licenseUrl"
			new URL(licenseUrl).withInputStream {
				outputStream << it
			}
		}
	}
}

task retrieveLicenses {
	licenses.each {
		dependsOn retrieveLicenseTaskName(it)
	}
}

task licensesTextsHtml {
	dependsOn retrieveLicenses
	inputs.file licensesDir
	outputs.file licensesTextsHtmlFile
	doLast {
		licensesTextsHtmlFile.delete()
		licenses.each {
			licensesTextsHtmlFile << createLicenseHtml(it)
		}
	}
}

android.applicationVariants.all { appVariant ->
	def generateImagesTaskName = "generateImages${appVariant.name}"
	def generateBuildInfoFileTaskName = "generateBuildInfoFile${appVariant.name}"
	def licensesHtmlTaskName = "licensesHtml${appVariant.name}"
	def licensesHtmlCleanupTaskName = "licensesHtmlCleanup${appVariant.name}"
	def resDir = file("build/res/all/${appVariant.baseName}")
	def assetsDir = file("build/assets/${appVariant.baseName}")
	task(generateImagesTaskName) {
		dependsOn appVariant.mergeResources
		doLast {
			generateImages(resDir)
		}
	}
	task(generateBuildInfoFileTaskName) {
		dependsOn appVariant.mergeResources
		doLast {
			generateBuildInfoFile(resDir)
		}
	}
	task(licensesHtmlTaskName) {
		dependsOn licensesTextsHtml
		inputs.file licensesTextsHtmlFile
		inputs.file licensesHtmlTemplateFile
		outputs.file "$assetsDir/licenses.html"
		doLast {
			copy {
				from licensesHtmlTemplateFile
				into assetsDir
				rename {
					'licenses.html'
				}
				filter(ReplaceTokens, tokens: ['license-texts': licensesTextsHtmlFile.text])
			}
		}
	}
	appVariant.processResources.dependsOn generateImagesTaskName, generateBuildInfoFileTaskName, licensesHtmlTaskName
}

eclipse {
	classpath {
		plusConfigurations += configurations.compile
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'src', path: 'src/main/java'])
				node.appendNode('classpathentry', [kind: 'src', path: 'build/source/r/debug'])
				node.appendNode('classpathentry', [kind: 'lib', path: "${androidSdkDir}/platforms/${androidTargetPlatform}/android.jar"])
			}
		}
	}
}
