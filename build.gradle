buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.3'
	}
}

ext.localProperties = new Properties();
file("local.properties").withInputStream { localProperties.load(it) }

def profileName = hasProperty('profile') ? profile : 'default'

apply from: "${profileName}.profile"

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'project-report'

	repositories {
		mavenCentral()
		mavenLocal()
	}

	group = 'org.juzidian'

	version = "${juzidianVersion}"

	plugins.withType(org.gradle.api.plugins.JavaBasePlugin) {
		eclipse.classpath.file {
			whenMerged { classpath ->
				// don't expose dependencies to other projects
				classpath.entries.findAll { entry -> entry.kind != 'output' }*.exported = false
			}
		}
	}

}

def getGitHeadCommit() {
	def out = new ByteArrayOutputStream();
	exec({
		executable 'git'
		args 'rev-parse', 'HEAD'
		standardOutput = out
		ignoreExitValue = true
	})
	return out.toString()
}

ext.buildInfo = [
	buildDate: new Date(),
	headCommit: getGitHeadCommit()
]

ext.libraries = [
	roboguice: dependencies.create('org.roboguice:roboguice:2.0') {
		exclude module: 'aopalliance'
		exclude module: 'cglib'
	},
	guice: 'com.google.inject:guice:3.0',
	slf4j_api: 'org.slf4j:slf4j-api:1.6.1',
	slf4j_simple: 'org.slf4j:slf4j-simple:1.6.1',
	slf4j_jdk14: 'org.slf4j:slf4j-jdk14:1.6.1',
	slf4j_android: 'org.slf4j:slf4j-android:1.6.1-RC1',
	ormlite_core: 'com.j256.ormlite:ormlite-core:4.48-NCJONES',
	ormlite_jdbc: 'com.j256.ormlite:ormlite-jdbc:4.48-NCJONES',
	ormlite_android: 'com.j256.ormlite:ormlite-android:4.48-NCJONES',
	sqlite_jdbc: 'org.xerial:sqlite-jdbc:3.7.2',
	javacc: 'net.java.dev.javacc:javacc:5.0',
	aws_sdk: 'com.amazonaws:aws-java-sdk:1.3.33',
	junit: 'junit:junit:4.11',
	hamcrest_library: 'org.hamcrest:hamcrest-library:1.3',
	mockito_core: 'org.mockito:mockito-core:1.8.5',
	url_scheme_registry: 'org.skife.url:url-scheme-registry:0.0.1'
]

