buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.2'
	}
}

ext.localProperties = new Properties();
file("local.properties").withInputStream { localProperties.load(it) }

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'project-report'

	repositories {
		mavenCentral()
	}

	group = 'org.juzidian'

	version = "${juzidianVersion}"

	plugins.withType(org.gradle.api.plugins.JavaBasePlugin) {
		eclipse.classpath.file {
			whenMerged { classpath ->
				// don't expose dependencies to other projects
				classpath.entries.findAll { entry -> entry.kind != 'output' }*.exported = false
			}
		}
	}

}

ext.libraries = [
	guice_no_aop: dependencies.create('com.google.inject:guice:3.0:no_aop') {
		exclude module: 'aopalliance'
		exclude module: 'cglib'
	},
	slf4j_api: 'org.slf4j:slf4j-api:1.6.1',
	slf4j_simple: 'org.slf4j:slf4j-simple:1.6.1',
	slf4j_jdk14: 'org.slf4j:slf4j-jdk14:1.6.1',
	slf4j_android: 'org.slf4j:slf4j-android:1.6.1-RC1',
	ormlite_core: 'com.j256.ormlite:ormlite-core:4.43',
	ormlite_jdbc: 'com.j256.ormlite:ormlite-jdbc:4.43',
	ormlite_android: 'com.j256.ormlite:ormlite-android:4.43',
	sqlite_jdbc: 'org.xerial:sqlite-jdbc:3.7.2',
	javacc: 'net.java.dev.javacc:javacc:5.0',
	junit: 'junit:junit:4.11+',
	hamcrest_library: 'org.hamcrest:hamcrest-library:1.3',
	mockito_core: 'org.mockito:mockito-core:1.8.5',
	url_scheme_registry: 'org.skife.url:url-scheme-registry:0.0.1'
]

