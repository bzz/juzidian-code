import java.io.FileInputStream
import java.io.FileNotFoundException
import java.io.InputStream
import java.sql.SQLException

import org.juzidian.cedict.CedictInputStreamProvider
import org.juzidian.cedict.CedictLoader
import org.juzidian.core.datastore.DbDictionaryDataStore
import org.juzidian.core.datastore.DbDictionaryDataStoreDbInitializer
import org.juzidian.core.datastore.DbDictionaryDataStoreEntryPopulator
import org.juzidian.core.inject.DictionaryModule

import com.google.inject.Guice
import com.google.inject.Injector
import com.j256.ormlite.jdbc.JdbcConnectionSource
import com.j256.ormlite.support.ConnectionSource

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath "org.juzidian:org.juzidian.cedict:${juzidianVersion}"
		classpath "org.juzidian:org.juzidian.core:${juzidianVersion}"
		classpath "org.juzidian:org.juzidian.pinyin:${juzidianVersion}"
		classpath libraries.guice_no_aop
		classpath libraries.ormlite_jdbc
		classpath libraries.sqlite_jdbc
		classpath libraries.slf4j_simple
	}
}

def cedictDataUrl = 'http://www.mdbg.net/chindict/export/cedict/cedict_1_0_ts_utf-8_mdbg.txt.gz'

def buildDir = "$projectDir/build"

def cedictDataPath = "$buildDir/cedict.dat"

def cedictTimestampPath = "$buildDir/cedict.timestamp"

def dictionaryDbPath = "$buildDir/juzidian_dictionary.db"

def compressedDictionaryDbPath = "$buildDir/juzidian_dictionary.db.gz"

def dbFormat = "${DictionaryDataBaseInfo.getDataBaseFormatVersion()}"

task clean << {
	project.delete("$buildDir")
}

task createBuildDir {
	outputs.dir "$buildDir"
	doLast {
		file("$buildDir").mkdirs()
	}
}

task fetchCedictData {
	dependsOn createBuildDir
	ext.srcUrl = "$cedictDataUrl"
	ext.destFile = file("$cedictDataPath")
	ext.timestampFile = file("$cedictTimestampPath")
	inputs.property 'srcUrl', srcUrl
	outputs.file destFile
	outputs.file timestampFile
	doLast {
		def outputStream = new FileOutputStream(destFile)
		println "Downloading CEDict data from $srcUrl"
		new URL("$srcUrl").withInputStream({
			outputStream << new java.util.zip.GZIPInputStream(it)
		})
		timestampFile.withWriter({
			it.println(new Date().getTime())
		})
	}
}

task generateDatabase {
	dependsOn fetchCedictData
	inputs.file file("$cedictDataPath")
	inputs.property 'dbFormat', dbFormat
	outputs.file file("$dictionaryDbPath")
	doLast {
		DictionaryDataBaseCreator.createDataBase(cedictDataPath, dictionaryDbPath)
	}
}

task compressDatabase {
	dependsOn generateDatabase
	inputs.file file("$dictionaryDbPath")
	outputs.file file("$compressedDictionaryDbPath")
	doLast {
		ant.gzip(src: "$dictionaryDbPath", destfile: "$compressedDictionaryDbPath")
	}
}

task generateChecksum {
	dependsOn compressDatabase
	inputs.file file("$compressedDictionaryDbPath")
	outputs.file file("${compressedDictionaryDbPath}.SHA1")
	doLast {
		ant.checksum(file: "$compressedDictionaryDbPath", algorithm: 'SHA1')
	}
}

task build {
	dependsOn generateChecksum
}

class DataBuildModule extends DictionaryModule {

	def jdbcUrl;

	public DataBuildModule(jdbcUrl) {
		this.jdbcUrl = jdbcUrl;
	}

	@Override
	protected ConnectionSource createConnectionSource() {
		return new JdbcConnectionSource(this.jdbcUrl);
	}

}

class DictionaryDataBaseCreator {

	static def createDataBase(cedictDataFile, dbFileName) {
		def jdbcUrl = "jdbc:sqlite:" + dbFileName
		def injector = Guice.createInjector(new DataBuildModule(jdbcUrl))
		def dbInitializer = new DbDictionaryDataStoreDbInitializer(
				new DbDictionaryDataStoreEntryPopulator(new CedictLoader(new CedictInputStreamProvider() {
					@Override
					public InputStream getInputStream() {
						return new FileInputStream(cedictDataFile)
					}
				})));
		def dictionaryDataStore = injector.getInstance(DbDictionaryDataStore.class)
		dbInitializer.initializeDb(dictionaryDataStore)
	}

}

class DictionaryDataBaseInfo {
	
	static def getDataBaseFormatVersion() {
		def injector = Guice.createInjector(new DataBuildModule("jdbc:sqlite::memory:"));
		def dataStore = injector.getInstance(DbDictionaryDataStore.class);
		return dataStore.getDataFormatVersion();
	}
	
}
